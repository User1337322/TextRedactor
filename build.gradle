plugins {
    // it's a java application. What did you expect?
    id 'java'
    id 'application'

    // jlink and jpackage plugins are always nice
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.1'
}

group('org.lucasstarsz')
version('0.2.0')

// Build

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.fxmisc.richtext:richtextfx:0.10.5'
}

application {
    mainModule.set('Compose.main')
    mainClass.set('org.lucasstarsz.composeapp.core.ComposeApp')
}

javafx {
    version = '15'
    modules = ['javafx.controls', 'javafx.fxml']
}

// Test

test {
    useJUnitPlatform()
}

// Package

boolean usingSnapshot = false
def snapshotVersion = "SNAPSHOT"
def distributionName = "Compose Text Editor${usingSnapshot ? "-$snapshotVersion" : ""}"

jlink {

    addExtraDependencies("javafx")
    options.addAll('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')

    launcher {
        name = distributionName
        noConsole = true
    }

    jpackage {
        // cross-platform options
        jvmArgs += ['-XX:+CreateCoredumpOnCrash']
        installerOptions += [
                '--description', 'A clean, simple text editor.',
                '--vendor', 'Andrew Dey',
                '--copyright', 'CopyrightÂ© 2020-2021 Andrew Dey',
                '--license-file', 'src/main/resources/LICENSE.txt',
                '--file-associations', 'src/main/resources/extensions/ext_txt.properties',
                '--file-associations', 'src/main/resources/extensions/ext_text.properties',
                '--app-version', project.version
        ]

        // platform-specific options
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def iconPath = 'src/main/resources/icons/compose'

        if (currentOs.windows) {
            installerType = "msi"
            icon = "${iconPath}_ico.ico"

            installerOptions += [
                    '--win-per-user-install',
                    '--install-dir', distributionName,
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu', '--win-menu-group', 'Compose Text Editor'
            ]
        } else if (currentOs.linux) {
            installerType = "deb"
            icon = "${iconPath}_icns.icns"

            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'andrewrcdey@gmail.com'
            ]
        } else if (currentOs.macOsX) {
            installerType = "pkg"
            icon = "${iconPath}_png.png"

            installerOptions += [
                    '--mac-package-name', 'Compose'
            ]
        }
    }
}
